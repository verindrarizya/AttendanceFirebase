import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {

    ext {
        compose_bom_version = "2024.02.02"
        firebase_bom_version = "33.1.2"
        kotlin_bom_version = "1.9.10"

        core_ktx_version = "1.10.1"
        lifecycle_version = "2.7.0"

        activity_version = "1.8.2"
        splashscreen_version = "1.0.1"

        navigation_compose_version = "2.8.0-beta02"

        datastore_version = "1.0.0"

        hilt_version = "2.46"

        hilt_navigation_compose_version = "1.2.0"

        lottie_compose_version = "6.1.0"

        coil_compose_version = "2.4.0"

        paging_version = "3.2.1"

        kotlin_serialization_version = "1.6.0"

        accompanist_permission_version = "0.35.1-alpha"

        camerax_version = "1.3.3"

        junit_version = "4.13.2"

        android_junit_version = "1.1.5"

        espresso_version = "3.5.1"
    }

    dependencies {
        // updating this to 4.4.1 will make module that contains google-services.json need to
        // become either an android module or dynamic-feature-module
        classpath 'com.google.gms:google-services:4.3.15'
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.10' apply false
    id 'com.google.dagger.hilt.android' version '2.46' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.10' apply false
}
subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("composeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
            if (project.findProperty("composeCompilerMetrics") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}